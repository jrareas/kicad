*-----------------------------------------------------------
*その他の素子
*-----------------------------------------------------------
*　　リスト
*
*    vc2  共振周波数へ追随するπ型共振回路用の２連ＶＣ
*    vc1  共振周波数へ追随するπ型共振回路用のＶＣ
*    dp0  ダイポールアンテナ
*    yag3  21MHzいい加減３素子八木アンテナ
*    vr0,vl0,vc0    電圧制御の受動素子（Ｒ、Ｌ、Ｃ）
*    lq0  単層巻のコイル     .ac  　用
*    lq1  単層巻のコイル     .tran  用
*    6c4  ３極菅　gm=5m moh μ=10 Rp=20K
*    xtal 水晶振動子
*    KFT82_43 KFT82_61 KFT50_43 KFT50_61 KFB801 KFB101 伝送線路トランス２線
*    KFB101-3  伝送線路トランス　３線
*    半理想オペアンプ　１２０dB　入力抵抗 1G
*
*-----------------------------------------------------------
*オペアンプ　１２０dB　入力抵抗 1G
*-----------------------------------------------------------
*                + - out
.subckt   opamp  1 2 3   params:   adb = 120
rin  1    2    1G
eout 3    0    value = { v(1,2)*pwr(10,adb/20) }
rout 3    0    1G
.ends
*-----------------------------------------------------------
*オペアンプ　１２０dB　入力抵抗 1G　電源制限
*-----------------------------------------------------------
*                + - V+ V- out
.subckt   opamp1 1 2 3  4  5  params:   adb = 120,     pi=3.14
rin  1    2    1G
eout 5 0 value={atan(pi*v(1,2)*pwr(10,adb/20)/v(3,4))*v(3,4)/pi-v(4)/2}
rpow 3    4    1G
.ends
*-----------------------------------------------------------
*共振周波数へ追随するπ型共振回路用のＶＣ
*２連ＶＣのタイプ。　.AC　では使える
*-----------------------------------------------------------
.subckt   vc2  1    2    params:   l=2.53uH
g0   1    2    laplace {v(1,2)} = { -2/(l*s) }
.ends
*-----------------------------------------------------------
*共振周波数へ追随するπ型共振回路用のＶＣ
*片方だけがＶＣのタイプ。　.AC　では使える
* in=ca0  coil=ld out=このVC1
*-----------------------------------------------------------
.subckt   vc1  1    2    params:   l=2.53uH  cin=200pF
g0   1    2    laplace {v(1,2)} = { -(cin*s)/(cin*l*s*s+1) }
.ends
*-----------------------------------------------------------
*ダイポールアンテナ
*１０ＭＨＺ　φ＝３ｍｍ　Ｌ＝０．２４７λで計算した値による
*８ＭＨｚ〜１２ＭＨｚで傾向は一致している
*中心周波数の共振などの用途に使うことしかできない
*title ＤＰ　１０ＭＨｚ
* 13.000000 MHz    166.736   717.730
* 12.000000 MHz    123.168   452.827
* 11.000000 MHz     92.002   218.536
* 10.000000 MHz     68.942    -4.240
* 9.000000  MHz     51.455  -225.198
* 8.000000  MHz     37.967  -459.851
* 7.000000  MHz     27.458  -717.871
* 6.000000  MHz     19.245 -1020.947
*-----------------------------------------------------------
.subckt   dp0  1    2    params:   f =10Meg
rd0  1    2    1Meg
cs   1    10   { 1/(1250*6.28*f) }
ls   10   2    { 1254/(6.28*f) }
rs   10   2    22714
.ends
*-----------------------------------------------------------
*    yag3 21MHzいい加減３素子八木アンテナ
*21MHz　φ＝32mm 
*20MHz-22.4MHz でシミュレーション出来る
*中心周波数の共振などの用途に使うことしかできない
* MHz   R       j
* 20.0  16.27   -56.01
* 20.2  17.10   -47.13
* 20.4  16.88   -39.26
* 20.6  15.19   -31.23
* 20.8  12.51   -22.12
* 21.0   9.59   -11.64
* 21.2   7.01     0.1 
* 21.4   5.14    12.81
* 21.6   4.18    26.99
* 21.8   4.19    39.93
* 22.0   5.13    53.76
* 22.2   6.92    67.49
* 22.4   9.41    80.99
*-----------------------------------------------------------
.subckt yag3    1    2    params:   f =21.2Meg
vs   1    3    0v
rd   4    2    1
ey3  3    4    freq { i(vs) } =
+(20.0Meg,58.3252,-73.8022)
+(20.2Meg,50.1362,-70.0579)
+(20.4Meg,42.7350,-66.7345)
+(20.6Meg,34.7282,-64.0621)
+(20.8Meg,25.4124,-60.5096)
+(21.0Meg,15.0816,-50.5155)
+(21.2Meg,7.01071,0.817287)
+(21.4Meg,13.8027,68.1369)
+(21.6Meg,27.3117,81.1964)
+(21.8Meg,40.1492,84.0096)
+(22.0Meg,54.0042,84.5491)
+(22.2Meg,67.8438,84.1457)
+(22.4Meg,81.5348,83.3726)
.ends
*-----------------------------------------------------------
*電圧制御の受動素子
*(1,2)制御電圧    (4,5)制御出力(R,L,C)
*(1,2) = (4,5) * param　となる　v1は掛け率が１となる電圧
*-----------------------------------------------------------
* 電圧可変抵抗
*------------------------
.subckt   vr0  1    2    4    5    params:   vs=1.0v   rs=1
rin  1    2    1G
rse  4    7    1n   ;ダミーの直列抵抗
eout 7    6    value = { v(1,2)*v(3,0)/vs }
fcop 0    3    vsen 1
vsen 6    5    0
rs0  3    0    { rs }
.ends
*------------------------
* 電圧可変インダクタンス
*------------------------
.subckt   vl0  1    2    4    5    params:   vs=1.0v   ls=1uH
rin  1    2    1G
rse  4    7    1n   ;ダミーの直列抵抗
eout 7    6    value = { v(1,2)*v(3,0)/vs }
fcop 0    3    vsen 1
vsen 6    5    0
ls0  3    0    { ls }
.ends
*------------------------
* 電圧可変キャパシタンス（容量）
*------------------------
.subckt   vc0  1    2    4    5    params:   vs=1.0v   cs=1uF init=0
rin  1    2    1G
ecop 3    6    value = { v(1,2)*v(4,5)/vs }
fout 4    5    vsen 1
vsen 0    6    0
cs0  3    0    { cs } ic={ init }
.ends

*-----------------------------------------------------------
*単層巻のコイル     Rac は周波数の√に比例して増加する
*                   H=インダクタンス F,Q=ある周波数でのＱ
* L+R(f) の直列形式、並列容量は無視される
* .AC でしか使えない
*-----------------------------------------------------------
.subckt   LQ0  t0   t1   params:   h=2.53uH  f0=10Meg,  q=80
l00  200  t1   { h }     ;インダクタンス
ri0  t0   100  1m        ;直列抵抗のダミー
rp0  100  200  1G        ;並列抵抗のダミー
gr0  100  200  value = { v(20)*q/80*f0/10Meg }    ;実効抵抗
rp1  20   0    1G        ;並列抵抗のダミー
er0  20   0    freq {v(t0,200)}     =             ;係数の算出
+  (     0,  13, 0)      ;DC
+  (  150k,  12, 0)
+  ( 10Meg,  -6, 0)
+  (100Meg, -16, 0)
+  (  1G  , -26, 0)
+  ( 10G  , -36, 0)
.ends
*-----------------------------------------------------------
*単層巻のコイル     Rac 　周波数の√に比例して増加する
*                   h 　　インダクタンス
*                   f0    Ｑを設定する周波数
*                   f1    動作させる　周波数
* L+R(f) の直列形式、並列容量は無視される
* 動作させる周波数では計算されたＱをもつ。
* .tran で使えるが .AC では不正確（抵抗分をあらかじめ計算するため）
*-----------------------------------------------------------
.subckt   lq1  t0   t1   params: h=2.53uH f0=10Meg q=80 f1=10Meg ic0=0mA
l00  t0   10   { h }     ic={ ic0 }     ;インダクタンス
rd0  10   t1   { (6.2832*f0*h)/q*sqrt(f1/f0) }
.ends
*-----------------------------------------------------------
*　３極菅　gm=5m moh μ=10 Rp=20K
*-----------------------------------------------------------
.subckt   6C4  P    G    K
*G K model
g_gk g    k    table {v(g,k)} = (-1, 0) (0, 0.1m) (1, 1m) (2,3m)
e_ef 1    0    table {v(g,k)+v(p,k)/10} = (0, 0) (1k, 1k)
r_ef 1    0    100meg           ;dumy
g_pk p    k    value = { 0.5m*pwr(v(1),1.5) }
c_gk g    k    10p
c_pg p    g    10p
c_pk p    k    5p
.ends
*-----------------------------------------------------------
*水晶振動子 Xtal  Fs=直列共振 Fp=並列共振-Fs Q = ZL/R
*-----------------------------------------------------------
* 例
* (TR 1984/8 p409) 10MHz cp=5.7pf  Rs=10 Cs=0.025p Ls=10mH
*”周波数と時間”電子情報通信学会 p70 5MHz cp=4pf  Rs=120 Cs=0.11fF Ls=9H
*                                     Q=2.3Meg
* Fs を直列共振周波数　Fd を並列共振周波数への距離　Cp を並列容量とする
.subckt Xtal t0 t1  params:   Fs=9.999Meg  Fd=1k Q=1000000 Cp=5.7pF
cp0  t0   t1   { Cp }
rs0  t1   10   { fs/(2*3.141516*Cp*Fd*Q*(Fd+2*Fs)) }
cs0  10   20   { cp*fd*(fd+2*fs)/(fs*fs) }
ls0  20   t0   { 1/(4*9.86960*cp*fd*(fd+2*fs)) }
rlek t0   t1   100Meg    ;Leakage
.ends
*-----------------------------------------------------------
*T=巻数　N＝重ね合わせ数　noise filter
*-----------------------------------------------------------
*noise filter    FT82 #43　
.subckt NFT82_43  ina   inb  outa outb     params:   T=5  N=1
L00  ina  5    { 557nH * T*T*N }
L01  inb  6    { 557nH * T*T*N }
K00  L00  L01  0.999
L10  5    outa .1uH
L11  6    outb .1uH
C01  ina  inb  1pF
C02  5    6    1pF
.ends
*-----------------------------------------------------------
*T=巻数　N＝重ね合わせ数　伝送線路の遅延係数は√２ p85 トロイダルコア活用法
*-----------------------------------------------------------
*伝送線路トランス       FT82 #43　
.subckt KFT82_43  ina   inb  outa outb     params:   T=5  N=1
L00  ina  5    { 557nH * T*T*N }
L01  inb  6    { 557nH * T*T*N }
K00  L00  L01  0.99999
T    5    6    outa outb z0=150    td={ (21*T-14)/300/0.7*N*1n   }
L10  5    outa .1uH
L11  6    outb .1uH
C01  ina  inb  1pF
C02  5    6    1pF
.ends
*-----------------------------------------------------------
*伝送線路トランス       FT82 #61　
* T=巻数 N=コアの数
*-----------------------------------------------------------
.subckt KFT82_61  ina   inb  outa outb     params:   T=5  N=1
L00  ina  5    { 80nH * T*T*N }
L01  inb  6    { 80nH * T*T*N }
K00  L00  L01  0.99999
T    5    6    outa outb z0=150    td={ (21*T-14)/300/0.7*N*1n   }
L10  5    outa .1uH
L11  6    outb .1uH
C01  ina  inb  1pF
C02  5    6    1pF
.ends
*-----------------------------------------------------------
*伝送線路トランス       FT50 #61　0.3mm twist 5turn(85mm)=0.283nsec
* T=巻数 N=コアの数
*-----------------------------------------------------------
.subckt KFT50_43  ina   inb  outa outb     params:   T=5  N=1
L00  ina  5    { 523nH * T*T*N }
L01  inb  6    { 523nH * T*T*N }
K00  L00  L01  0.99999
T    5    6    outa outb z0=150    td={ (20*T-12)/300/0.7*N*1n   }
L10  5    outa .1uH
L11  6    outb .1uH
C01  ina  inb  1pF
C02  5    6    1pF
.ends
*-----------------------------------------------------------
*伝送線路トランス       FT50 #61　0.3mm twist 5turn(85mm)=0.283nsec
* T=巻数 N=コアの数
*-----------------------------------------------------------
.subckt KFT50_61  ina   inb  outa outb     params:   T=5  N=1
L00  ina  5    { 68nH * T*T*N }
L01  inb  6    { 68nH * T*T*N }
K00  L00  L01  0.99999
T    5    6    outa outb z0=150    td={ (20*T-12)/300/0.7*N*1n   }
L10  5    outa .1uH
L11  6    outb .1uH
C01  ina  inb  1pF
C02  5    6    1pF
.ends
*-----------------------------------------------------------
*伝送線路トランス       FB801 #43 8mm AL=1565nH/T^2 T=巻数 N=コアの数
*-----------------------------------------------------------
.subckt KFB801  ina   inb  outa outb params:   T=5     N=1
L00  ina  5    { 1565nH * T*T*N }
L01  inb  6    { 1565nH * T*T*N }
K00  L00  L01  0.99999
T    5    6    outa outb z0=150    td={ (20*T-12)/300/0.7*N*1n   }
L10  5    outa .1uH
L11  6    outb .1uH
C01  ina  inb  1pF
C02  5    6    1pF
.ends
*-----------------------------------------------------------
*伝送線路トランス       FB101 #43 3　ビーズ型 T=巻数 N=コアの数
*-----------------------------------------------------------
.subckt KFB101  ina   inb  outa outb params:   T=5     N=1
L00  ina  5    { 609nH * T*T*N }
L01  inb  6    { 609nH * T*T*N }
K00  L00  L01  0.99999
T    5    6    outa outb z0=150    td={ (9*T-3)/300/0.7*N*1n   }
L10  5    outa .1uH
L11  6    outb .1uH
C01  ina  inb  1pF
C02  5    6    1pF
.ends
*-----------------------------------------------------------
*３本巻の伝送線路風トランス       FB101 #43 3　ビーズ型 T=巻数 N=コアの数
*-----------------------------------------------------------
.subckt   KFB101-3  ina  inb  inc  outa outb outc params:   T=5  N=1
L00  ina  5    { 609nH * T*T*N }
L01  inb  6    { 609nH * T*T*N }
L02  inc  7    { 609nH * T*T*N }
K00  L00  L01  L02  0.999
T0   5    0    outa 0    z0=150    td={ (9*T-3)/300/0.7*N*1n   }
T1   6    0    outb 0    z0=150    td={ (9*T-3)/300/0.7*N*1n   }
T2   7    0    outc 0    z0=150    td={ (9*T-3)/300/0.7*N*1n   }
L10  5    outa .1uH
L11  6    outb .1uH
L12  7    outc .1uH
C00  ina  inb  1pF
C01  inb  inc  1pF
C02  ina  inc  1pF
C10  5    6    1pF
C11  6    7    1pF
C12  5    7    1pF
.ends
*-----------------------------------------------------------
*Dousou トランス    H = mH    M=ketugou
*-----------------------------------------------------------
.subckt KDT  ina   inb  outa outb     params:   H=30mH M=0.999
L00  ina  5    { H }
L01  inb  6    { H }
K00  L00  L01  { M }
L10  5    outa 1uH       ;zanryuu induct
L11  6    outb 1uH
C01  ina  inb  10pF      ;float capacita
C02  5    6    10pF
.ends
*-------------------------------------------------------------
*200V Valister
*-------------------------------------------------------------
.subckt DVALI  a    b    ;params:   V=200V R=1 C=10pF
.model	dvali200 D(IS=0.5uA RS=2 BV=200 IBV=0.5uA
+                   CJO=18pF Vj=14.865 M=2.341)
D1   a    1    dvali200
D2   b    1    dvali200 
C1   a    b    10pF
.ends
*------------------------
* サンプルホルド
*------------------------
*              clk  in   out
.subckt   sh   clk  in   out
ein  10   0   value = { v(in) }
.model  shsw    vswitch(RON=1, ROFF=1G, VON=1, VOFF=0)
sw1  10   12    clk 0   shsw
chld 12   0   1p
eout out  0    value = { v(12) }
rout    out 0   1G
.ends
*------------------------
* サンプルホルド＋シフトレジスタのパーツ
*------------------------
*              clk  in+ out
.subckt   shp  3    1   2
ein  10   0   value = { v(1) }
.model  shsw1 vswitch(RON=1, ROFF=1G, VON=0   , VOFF=0.25)
.model  shsw2 vswitch(RON=1, ROFF=1G, VON=1   , VOFF=0.75)
sw1  10   12   3   0   shsw2
chl1 12    0   100p
;e1   20    0   value = { v(12) }
sw2  12   22   3   0   shsw1
chl2 22    0   0.1p
e2   2     0   value = { v(22) }
rd  2   0   1G
.ends
*------------------------
* 4 段サンプルホルド
*------------------------
*              clk  in+ out...
.subckt   sh4  1    2   3   4   5   6
x1p 1   2   3   shp
x2p 1   3   4   shp
x3p 1   4   5   shp
x4p 1   5   6   shp
.ends
*-----------------------------------------------------------
* TC4066B アナログＳＷ
*------------------------
.subckt tc4066b a1  a2  ctl GND  
rpin    ctl GND 100Meg
rsin    ctl 1   2k
cin     1   GND 30p
.model  shsw2 vswitch(RON=2k, ROFF=1G, VON=2   , VOFF=1.75)
sw1  a1  a2  1  GND shsw2
.ends
*-----------------------------------------------------------
* 0-5V CMOS inverter
*------------------------
.subckt inv in out
rsin    in  0   10meg
ep0     out 0   value = { 5 - v(in) }
.ends
*-----------------------------------------------------------
*-----------------------------------------------------------
*-----------------------------------------------------------
*all end
*-----------------------------------------------------------

