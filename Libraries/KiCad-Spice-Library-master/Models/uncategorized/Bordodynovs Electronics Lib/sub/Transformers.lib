.subckt CoreJA  100  params: ms=381.82k a=28.067 k=18.57 c=0.24753
+ area=24u  LeN=32m
.Subckt Winding1 l1 l2 mmf_out2 mmf_out
.param   Tau=50n 
B1 l1 l2 v=v(7)
bH2  mmf_out 6 v=i(B1)
v2  6 mmf_out2  dc  0
BF  7   0 i=i(v2) tripdt=50n
L  7  0  1 Rpar={1/tau} Cpar=.05f
.ends Winding1 
*
.param RS=1m
.param c1={1/(1+c)} c2={c1*C}
.param mU0=1.256637061436u
.func f(x) {IF(aBS(x)>1m,(1/taNh(x)-1/x),0.33333*x)}
b1 3 0 V=V(m)/LeN
b2 4 0  V=f(v(3)/A)
b3 5 0 v=v(m)
C1 5 7 1m rser={rs}  ic=0
V1 7 0 0
bi4 0 9 i=k*SGN(I(V1))  rpar=1 Cpar=50n
*b5 11 0 v=Uramp(V(4,12)/((V(9)+1m)*len))
b5 0 11 i=Uramp(V(4,12)/((V(9)+1m)*len)) Rpar=1 Cpar=50n
b6 12 0 V=idt(1000*I(V1)*v(11)*c1)+c2*V(4)  tripdt=50n
b7 13 0 V=aRea*mU0*(mS*V(12)+V(3))
bi1 m 0 i=v(13)
xw 100 0 m 0 Winding1
.ends CoreJA

.subckt CoreJA_GAP 100 params: ms=381.82k a=28.067 k=18.57 c=0.24753 
+ area=24u  LeN=32m gap=5u
.param rgap={gap/(1.256637061436u*AREA)}
.Subckt Winding1 l1 l2 mmf_out2 mmf_out
.param   Tau=50n 
B1 l1 l2 v=v(7)
bH2  mmf_out 6 v=i(B1)
v2  6 mmf_out2  dc  0
BF  7   0 i=i(v2) tripdt=50n
L  7  0  1 Rpar={1/tau} Cpar=.05f
.ends Winding1 
*
.param RS=1m
.param c1={1/(1+c)} c2={c1*C}
.param mU0=1.256637061436u
.func f(x) {IF(aBS(x)>1m,(1/taNh(x)-1/x),0.33333*x)}
b1 3 0 V=V(m)/LeN
b2 4 0  V=f(v(3)/A)
b3 5 0 v=v(m)
C1 5 7 1m rser={rs}  ic=0
V1 7 0 0
bi4 0 9 i=k*SGN(I(V1))  rpar=1 Cpar=50n
*b5 11 0 v=Uramp(V(4,12)/((V(9)+1m)*len))
b5 0 11 i=Uramp(V(4,12)/((V(9)+1m)*len)) Rpar=1 Cpar=50n
b6 12 0 V=idt(1000*I(V1)*v(11)*c1)+c2*V(4)  tripdt=50n
b7 13 0 V=aRea*mU0*(mS*V(12)+V(3))
bi1 m 0 i=v(13)
xw 100 0 m gap Winding1
Rgap gap 0 {Rgap}
.ends CoreJA_GAP


.subckt Winding_LCR 1 2 core Lser=1n Rser=1m  Cpar=1p N=1
L 2 3 {Lser} Rser={Rser}
C 1 2 {Cpar}
E 1 3 core 0 {N}
F 0   core E {N}
Rp core 0 1T
Cp 2 0 1p Rpar=1T
.ends Winding_LCR

.subckt Winding_RC 1 2 core  Rser=1m  Cpar=1p N=1
R 2 3 {Rser}
C 1 2 {Cpar}
E 1 3 core 0 {N}
F 0   core E {N}
Rp core 0 1T
Cp 2 0 1p Rpar=1T
.ends Winding_RC

.subckt core core Hc=5 Bs=460m Br=100m A=14u Lm=31m Lg=0 Fe=10Meg
.param u0u={2*Bs*Br/(Bs+Br)/Hc}
.param L={u0u*A/Lm/(1+u0u*Lg/Lm)}
.param Rpar={L*2*Pi*Fe}
L core 0 Hc={Hc} Bs={Bs} Br={Br} A={A} Lm={Lm} Lg={Lg} N=1 Rser=0 Rpar={Rpar}
.ends core

.subckt core_H_B core  H B Hc=5 Bs=460m Br=100m A=14u Lm=31m Lg=0 Fe=10Meg
.param u0u={2*Bs*Br/(Bs+Br)/Hc}
.param L={u0u*A/Lm/(1+u0u*Lg/Lm)}
.param Rpar={L*2*Pi*Fe}
Vam core core0 0
L core0 0 Hc={Hc} Bs={Bs} Br={Br} A={A} Lm={Lm} Lg={Lg} N=1 Rser=0 Rpar={Rpar}
B1 H 0 v=i(Vam)/Lm
B2 B 0 v=idt(v(core0))/A 
.ends core_H_B

.subckt coreLin_AL core  AL=1u  Fe=100Meg
L core 0 {AL} Rser=0 Rpar={2*Pi*Fe*AL}
.ends corecoreLin_AL

.subckt coreLin_A_Lm core  mU=2000 A=14u Lm=31m Lg=0 Fe=10Meg
.param AL={1.2566e-6*A/Lm*(mU/(1+mU*Lg/Lm))}
L core 0 {AL} Rser=0 Rpar={2*Pi*Fe*AL}
.ends coreLin_A_Lm

.subckt coresat core   params:  IVSEC=0 Ae=14u Lm=31m Lg=0
+  Bsat=350m ur=2000  Hc=50 Feddy=250k
*
.param VSEC=Ae*Bsat
.param u0=1.25u
.param u={u0*ur/(1+ur*(lg/lm))}
.param Lmag={u*Ae/lm}
.param Lsat={u0*Ae/(lm+lg)}
.param IHyst={Hc*lm}
.param Cjo={3*VSEC/(6.28*Feddy*clamp*Lmag)}
.param clamp=250
*
Rdum phi 0 10G
F1 core 12 VM 1
Gint 0 phi 12 core  -1
C1 phi 0 {VSEC/Clamp} IC={IVSEC/VSEC*clamp}
Ebuf 5 0 phi 0 1
Rmag 8 0 {Lmag*clamp/VSEC}
VM 5 8
D3 2 9 D2mod
V6 9 0 DC={Clamp}
R2 2 8 {Lsat*clamp/VSEC}
V8 0 10 DC={Clamp}
Vdum 12 0
D4 10 2 D2mod
I1 6 core DC={IHYST}
I6 6 12 DC={IHYST}
D1 core 6 Dmod
D2 12 6 Dmod
.MODEL Dmod D 
.MODEL D2mod D CJO={Cjo} VJ=25
.ENDS

