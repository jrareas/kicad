* LM35 spice model by ACMattos Apr-2009
* See LM35 datasheet


.subckt LM35 Out v+ v-

V1 N002 v- {temp*0.01}
XU1 N003 N001 v+ v- N001 level.2 Avol=1Meg GBW=10Meg Slew=10Meg ilimit=25m rail=0 Vos=0 phimargin=45 en=0 enk=0 in=0 ink=0 Rin=500Meg
I1 v+ v- {56u+0.39u*temp}
R1 out N001 0.5
R3 N003 N002 10k
R4 N001 v- 10k

.subckt level.2 1 2 3 4 5
S1 5 3 N002 5 Q
S2 4 5 5 N002 Q
A1 2 1 0 0 0 0 N002 0 OTA G={Avol/Rout} ref={Vos} Iout={slew*Cout} Cout={Cout} en={en} enk={enk} in={in} ink={ink} incm={incm} incmk={incmk} Vhigh=1e308 Vlow=-1e308
C3 5 4 1p
C4 3 5 1p
R2 N002 4 {2*Rout} noiseless
R1 3 N002 {2*Rout} noiseless
R3 3 1 {2*Rin} noiseless
R4 3 2 {2*Rin} noiseless
R5 2 4 {2*Rin} noiseless
R6 1 4 {2*Rin} noiseless
G1 0 N002 4 N002 table(0 0 10 {2*slew*Cout})
G2 N002 0 N002 3 table(0 0 10 {2*slew*Cout})
.param Rout=100Meg
.param Cout={Avol/GBW/2/pi/Rout}
.model Q SW(Ron=10 Roff=10Meg Vt=0 Vh=-.1 Vser={Rail} ilimit={Ilimit} noiseless level=2 epsilon={Rail/10})
.param Avol=1Meg GBW=10Meg Slew=10Meg ilimit=25m rail=0 Vos=0
.param en=0 enk=0 in=0 ink=0 incm=0 incmk=0 Rin=1G
.ends level.2 
.ends


