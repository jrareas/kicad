
.SUBCKT LM3485_STARTUP ISEN GND FB ADJ PGATE VIN
BE1 VIN VCC V=IF(V(VIN)<9,(V(VIN)-0),8.4)
XFB FB REFLO COMPLO COMPWDEL_0
XFB2 FB REFHI COMPHI COMPWDEL_0
XFB3 ADJ ISEN ILIM COMPWDEL_2
BG2 ADJ GND I=5.5u
XA3 ILIM 32 ONESHOT AND2A
XA4 PWM 32 INVERTERA4
X3 ONESHOT RESET Q QN RSFF
RGATE PGATEo PGATE 20
BE4 PGATEo GND V=IF(V(DRIVE)>1,V(VIN),V(VCC))
XA5 Q COMP PWM OR2A5
X6 RESET GND QN GND SSWITCH
BG6 0 RESET I=IF(V(Q)>2.5,2m,0)
C7 RESET GND 8n IC=5
D2 GND RESET DN751
.MODEL DN751 D BV=4.9458 CJO=406.80P IBV=10MA IS=1E-11 M=.33 N=1.27 RS=7.7083 TT=50N VJ=.75
V1 REFHI GND DC=1.247
V2 REFLO GND DC=1.237
XA1 COMPLO 17 COMP AND2A
XA2 COMPHI COMP 17 OR2A3
R5 DRIVE PWM 1m
.ENDS LM3485_STARTUP
* LM3485


.SUBCKT COMPWDEL_0 1 2 3 PARAMS: TD=190N
BE1 4 0 V=IF(V(1)>V(2),5,0)
T1 4 0 5 0 Z0=1 TD={TD}
R1 5 0 1
BE2 3 0 V=IF(V(5)<0,0,V(5))
.ENDS COMPWDEL_0
*$
.SUBCKT COMPWDEL_2 1 2 3 PARAMS: TD=10N
BE1 4 0 V=IF(V(1)>V(2),5,0)
T1 4 0 5 0 Z0=1 TD={TD}
R1 5 0 1
BE2 3 0 V=IF(V(5)<0,0,V(5))
.ENDS COMPWDEL_2
*$
.SUBCKT RSFF SET RESET Q QN
*A1 1 2 3 SET_DIN RESET_DIN Q_DOUT QN_DOUT SRLATCH2A1
BEA1_Q 5 0 V=IF(V(SET)>2.5,5,IF(V(RESET)>2.5&V(SET)<0.1,0.1,V(Q)))
BEA2_QN 6 0 V=IF(V(SET)>2.5,0.1,IF(V(RESET)>2.5&V(SET)<0.1,5,V(QN)))
RQ 5 Q 1
CQ Q 0 1e-15 IC=100m
RQN 6 QN 1
CQN QN 0 1e-15 IC=5
.ENDS
*$
.SUBCKT SSWITCH 1 2 3 4 PARAMS: RON=1 ROFF=1MEG VON=5 VOFF=0
*CONNECTIONS + - NC+ NC-
*PARAMETERS: RON ON RESISTANCE IN OHMS, ROFF OFF RESISTANCE IN OHMS
* VON ON CURRENT IN AMPS, VOFF OFF CURRENT IN AMPS
* IF V(3,4) > VON THEN RSWITCH=RON, IF V(3,4) < VOFF THEN RSWITCH=ROFF, ELSE
* RSWITCH WILL SMOOTHLY TRANSISTION BETWEEN THE ON AND OFF STATES
S_A1 1 2 3 4 SMOOTH
.MODEL SMOOTH VSWITCH RON={RON} ROFF={ROFF} VON={VON} VOFF={VOFF}
.ENDS SSWITCH
*$
.SUBCKT AND2A A B Y PARAMS: VDD=5 VSS=0 VTHRESH=2.5
BE_ABMGATE 0 Y i=IF(V(A)>{VTHRESH}&V(B)>{VTHRESH},{VDD},{VSS}) Rpar=1 Cpar=3n
.ENDS AND2A
*$
.SUBCKT INVERTERA4 A Y PARAMS: VDD=5 VSS=0 VTHRESH=2.5
BE_ABMGATE 0 Y i=IF(V(A)>{VTHRESH},{VSS},{VDD}) Rpar=1 Cpar=0.8n
.ENDS INVERTERA4
*$
.SUBCKT OR2A5 A B Y PARAMS: VDD=5 VSS=0 VTHRESH=2.5
BE_ABMGATE 0 Y i=IF(V(A)>{VTHRESH}|V(B)>{VTHRESH},{VDD},{VSS}) Rpar=1 Cpar=1f
.ENDS OR2A5
*$
.SUBCKT OR2A3 A B Y PARAMS: VDD=5 VSS=0 VTHRESH=2.5
BE_ABMGATE 0 Y i=IF(V(A)>{VTHRESH}|V(B)>{VTHRESH},{VDD},{VSS}) Rpar=1 Cpar=1n
.ENDS OR2A3
*$
.SUBCKT LDCR 1 2 PARAMS: IC=0 L=1.5E-5 DCR=0.04
*{ L = 1.5E-5 DCR = 0.04 }
* PARAMETERS: L INDUCTANCE IN HENRIES, DCR DC SERIES RESISTANCE IN OHMS
L1 2 1 1.5E-5 Rser={ DCR } IC = { IC }
.ENDS LDCR
*$

