*
.subckt TO132-40-12 A K MT2 MT1
Ru MT2 MT1 400k
XU1 MT2 VG MT1 TO132-40-12_TRIAC
R2 VG MT1 10K
D1 A 3 IRLED
D4 A K IRLEDcap
V3 3 K 0
B1 MT1 VG I=V(VIP)
* B1 MT1 VG I={V(VIP)} ; MULTISIM
C1 VIP 0 3.5n
R4 VIP 1 1.45K
D2 VIP 1 DIDEAL
B2 1 0 V=1.25*I(V3)
* B2 1 0 V={1.25*I(V3)} ; MULTISIM
D3 2 1 DZ
R3 VIP 2 1.51K
*
.subckt TO132-40-12_TRIAC A G K
*
X1 A K G Triac_ST params:
+ Vdrm=1200v
+ Igt=50ma
+ Ih=80ma
+ Rt=5.1m
+ Standard=1
.ends TO132-40-12_TRIAC
*
***************************************************
.subckt Triac_ST A K G PARAMS:
+ Vdrm=400v
+ Igt=20ma
+ Ih=6ma
+ Rt=0.1
+ Standard=1
*
* Vdrm : Repetitive forward off-state voltage
* Ih   : Holding current
* Igt  : Gate trigger current
* Rt   : Dynamic on-state resistance
* Standard : Differenciation between Snubberless and Standard Triacs
* (Standard=0 => Snubberless Triacs, Standard=1 => Standard Triacs)
*
*****************
* Power circuit *
*****************
*
****************
*Switch circuit*
****************
* Q1 & Q2 Conduction
S_S3         A Plip1 positive 0 Smain
*RS_S3        positive 0 1G
D_DAK1         Plip1 Plip2 Dak
R_Rlip         Plip1 Plip2  1k
V_Viak         Plip2 K DC 0 AC 0
*
* Q3 & Q4 Conduction
S_S4         A Plin1 negative 0 Smain
*RS_S4        negative 0 1G
D_DKA1         Plin2 Plin1 Dak
R_Rlin         Plin1 Plin2  1k
V_Vika         K Plin2 DC 0 AC 0
**************
*Gate circuit*
**************
R_Rgk         G K  10G
D_DGKi         Pg2 G Dgk
D_DGKd         G Pg2 Dgk
V_Vig         Pg2 K DC 0 AC 0
R_Rlig         G Pg2  1k
*******************
*Interface circuit*
*******************
* positive pilot
R_Rp         Controlp positive  2.2
C_Cp         0 positive  1u
E_IF15OR3         Controlp 0 VALUE {IF(  ( (V(CMDIG)>0.5) | (V(CMDILIH)>0.5) |
+ (V(CMDVdrm)>0.5) ),400,0 )}
*
* negative pilot
R_Rn         Controln negative  2.2
C_Cn         0 negative  1u
E_IF14OR3         Controln 0 VALUE {IF(  ( (V(CMDIG)>0.5) | (V(CMDILIHN)>0.5) |
+ (V(CMDVdrm)>0.5) ),400,0 )}
*
*
******************
* Pilots circuit *
******************
******************
* Pilot Gate     *
******************
E_IF1IG         inIG 0 VALUE {IF(  ( ABS(I(V_Vig)) ) > Igt ,1,0 )}
E_MULT2MULT         CMDIG 0 VALUE {V(Q4)*V(inIG)}
E_IF2Quadrant4         Q4 0 VALUE
+ {IF(((I(V_Vig)>Igt)&((V(A)-V(K))<0)&(Standard==0)),0,1)}
*
******************
* Pilot IHIL     *
******************
*
E_IF10IL         inIL 0 VALUE {IF( ((I(V_Viak))>2.5*Igt),1,0 )}
E_IF5IH         inIH 0 VALUE {IF( ((I(V_Viak))>(Ih/3)),1,0 )}
*
* Flip_flop IHIL
E_IF6DIHIL         SDIHIL 0 VALUE {IF(
+ (V(inIL)*V(inIH)+V(inIH)*(1-V(inIL))*(V(CMDILIH)) )>0.5,1,0)}
C_CIHIL         CMDILIH 0  1n
R_RIHIL         SDIHIL CMDILIH  1K
R_RIHIL2         CMDILIH 0  100Meg
******************
* Pilot IHILN    *
******************
*
E_IF11ILn         inILn 0 VALUE {IF( ((I(V_Vika))>2.5*Igt),1,0 )}
E_IF3IHn         inIHn 0 VALUE {IF( ((I(V_Vika))>(Ih/3)),1,0 )}
* Flip_flop IHILn
E_IF4DIHILN         SDIHILN 0 VALUE {IF(
+ (V(inILn)*V(inIHn)+V(inIHn)*(1-V(inILn))*(V(CMDILIHN)) )>0.5,1,0)}
C_CIHILn         CMDILIHN 0  1n
R_RIHILn         SDIHILN CMDILIHN  1K
R_RIHILn2         CMDILIHN 0  100Meg
******************
* Pilot VDRM     *
******************
E_IF8Vdrm         inVdrm 0 VALUE {IF( (ABS(V(A)-V(K))>(Vdrm*1.3)),1,0 )}
E_IF9IHVDRM         inIhVdrm 0 VALUE {IF( (I(V_Viak)>(Vdrm*1.3)/1.2meg)|
+  (I(V_Vika)>(Vdrm*1.3)/1.2meg),1,0)}
* Flip_flop VDRM
E_IF7DVDRM         SDVDRM 0 VALUE {IF(
+ (V(inVdrm)+(1-V(inVdrm))*V(inIhVdrm)*V(CMDVdrm) )>0.5,1,0)}
C_CVdrm         CMDVdrm 0  1n
R_RVdrm         SDVDRM CMDVdrm  100
R_RVdrm2        CMDVdrm 0  100Meg
****************
* Switch Model *
****************
*.MODEL Smain VSWITCH Roff=1.2meg Ron={Rt} Voff=0 Von=100
.MODEL Smain SW Roff=1.2meg Ron={Rt} Von=50.5 Vh=49.5
****************
* Diodes Model *
****************
.MODEL  Dak D( Is=3E-12 Cjo=5pf)
.MODEL  Dgk D( Is=1E-16 Cjo=50pf Rs=5)
.ends Triac_ST
*
****************
*
.MODEL IRLED D (IS=22P RS=84M N=2.375
+  VJ=.75 M=.333 TT=4.32U)
.MODEL IRLEDcap D (IS=22P RS=84M N=2.375 BV=6 IBV=10n
+ CJO=35P VJ=.75 M=.333)
*
.MODEL DZ D (IS=2.6p RS=0.01 N=1.206 EG=0.6
+XTI=3.127 BV=13.5 IBV=1e-05 CJO=1e-11 VJ=0.7 M=0.5 FC=0.5 TT=1e-09)
*
.model DIDEAL D(Vfwd=100u Epsilon=2m)
* .model DIDEAL D (is=1n n=0.0002) ; MULTISIM
.ends
