* Source File: DG417Blk.asc
* Developed: ETech (eetech00@yahoo.com)
* Created: Feb 5 2017
* Revision: NA
* This DG417 spice behavioral model was tested
* with LTSpice. Temperature is not modeled.
*
* Vishay Siliconix 
* DG417 Precision CMOS Analog Switche SPST NC (Logic 0=ON 1=OFF)
*
* block symbol definitions
.subckt DG417 IN D S VL VG VP VN
R1 IP IN 10
XX1 IP IPLS VP VN inprot
XX2 IPLS OUT1 OUT2 VL VG VP VN lvlshiftblk
XX3 OUT2 1S 1D VP VN swbbmblk
XX5 1S S VP VN inprot
XX6 1D D VP VN inprot
.ends DG417

.subckt inprot IN OUT VDD VSS
D1 IN VDD Di
D2 VSS IN Di
R1 OUT IN 10m
.MODEL Di D (IS=10p RS=10 N=1.75 M=0.4 Cjo=0.6p TT=20n)
.ends inprot

.subckt lvlshiftblk IN OUT1 OUT2 VL VG VP VN
MN1 NIN IN VG VG NA L=10u W=170u Ad=8500p As=8500p Pd=440u Ps=440u NRD=0.1 NRS=0.1
MP1 NIN IN N001 VP PA L=10u W=360u Ad=18000p As=18000p Pd=820u Ps=820u NRS=0.54 NRD=0.54
MN2 OUTB IN VG VG NA L=10u W=170u Ad=8500p As=8500p Pd=440u Ps=440u NRD=0.1 NRS=0.1
MN3 OUTA NIN VG VG NA L=10u W=170u Ad=8500p As=8500p Pd=440u Ps=440u NRD=0.1 NRS=0.1
MP2 OUTB OUTA VP VP PA l=10u w=360u ad=18000p as=18000p pd=820u ps=820u m={M23} nrd=0.54 nrs=0.54
MP3 OUTA OUTB VP VP PA l=10u w=360u ad=18000p as=18000p pd=820u ps=820u m={M23} nrd=0.54 nrs=0.54
MP4 O2 OUTA VP VP PA L=10u W=360u Ad=18000p As=18000p Pd=820u Ps=820u NRS=0.54 NRD=0.54
MP5 O1 OUTB VP VP PA L=10u W=360u Ad=18000p As=18000p Pd=820u Ps=820u NRS=0.54 NRD=0.54
MN5 O1 O2 VN VN NA l=10u w=170u ad=8500p as=8500p pd=440u ps=440u m={M45} nrd=0.1 nrs=0.1
MN4 O2 O1 VN VN NA l=10u w=170u ad=8500p as=8500p pd=440u ps=440u m={M45} nrd=0.1 nrs=0.1
MN6 OUT1 O2 VN VN NA L=10u W=170u Ad=8500p As=8500p Pd=440u Ps=440u NRD=0.1 NRS=0.1
MP6 OUT1 O2 VP VP PA L=10u W=360u Ad=18000p As=18000p Pd=820u Ps=820u NRS=0.54 NRD=0.54
D1 VL VP Di
R1 VL N001 500
MN7 OUT2 O1 VN VN NA L=10u W=170u Ad=8500p As=8500p Pd=440u Ps=440u NRD=0.1 NRS=0.1
MP7 OUT2 O1 VP VP PA L=10u W=360u Ad=18000p As=18000p Pd=820u Ps=820u NRS=0.54 NRD=0.54
C1 VP O2 {CD} Rpar=10G
C2 O2 VN {CD} Rpar=10G
C3 VP O1 {CD} Rpar=10G
C4 O1 VN {CD} Rpar=10G
.MODEL Di D (IS=10p RS=10 N=1.75 M=0.4 Cjo=6p TT=20n)
.MODEL NA NMOS (LEVEL=49
+VERSION=3.1 CAPMOD=2 MOBMOD=1
+TOX=4E-8 XJ=2.9E-7 NCH=4E15 NSUB=5.33E15 XT=8.66E-8 
+VTH0=1.4 U0= 1300 WINT=2.0E-7 LINT=1E-7 
+NGATE=5E20 RSH=5 JS=3.23E-8 JSW=3.23E-8 CJ=6.8E-8 MJ=0.5 PB=0.95
+CJSW=1.26E-10 MJSW=0.5 PBSW=0.95 PCLM=5
+CGSO=3.4E-10 CGDO=3.4E-10 CGBO=5.75E-10)
.MODEL PA PMOS (LEVEL=49
+VERSION=3.1 CAPMOD=2 MOBMOD=1
+TOX=5E-8 XJ=2.26E-7 NCH=1E15 NSUB=8E14 XT=8.66E-8
+VTH0=-1.65 U0= 400 WINT=1.0E-6 LINT=1E-6
+NGATE=5E20 RSH=15 JS=3.51E-8 JSW=3.51E-8 CJ=5.28E-8 MJ=0.5 PB=0.94
+CJSW=1.19E-10 MJSW=0.5 PBSW=0.94 pCLM=5
+CGSO=4.5E-10 CGDO=4.5E-10 CGBO=5.75E-10)
.param CD=0.15p
.param M23=0.01
.param M45=0.3
.ends lvlshiftblk

.subckt swbbmblk IN S D VP VN
MN3 S IN2 D BN NA l=10u w=170u ad=8500p as=8500p pd=440u ps=440u m=4 nrd=0.1 nrs=0.1
MN4 S IN2 BN BN NA L=10u W=170u Ad=8500p As=8500p Pd=440u Ps=440u NRD=0.1 NRS=0.1
MN5 BN IN1 VN VN NA L=10u W=170u Ad=8500p As=8500p Pd=440u Ps=440u NRD=0.1 NRS=0.1
MP3 D IN1 S VP PA l=10u w=360u ad=18000p as=18000p pd=820u ps=820u m=4 nrd=0.54 nrs=0.54
MN2 IN2 IN1 VN VN NA L=10u W=170u Ad=8500p As=8500p Pd=440u Ps=440u NRD=0.1 NRS=0.1
MP2 IN2 IN1 VP VP PA L=10u W=360u Ad=18000p As=18000p Pd=820u Ps=820u NRS=0.54 NRD=0.54
C1 S IN2 1e-15 Rser=1m Rpar=10G
C2 IN2 D 1e-15 Rser=1m Rpar=10G
MN1 IN1 IN VN VN NA L=10u W=170u Ad=8500p As=8500p Pd=440u Ps=440u NRD=0.1 NRS=0.1
MP1 IN1 IN VP VP PA L=10u W=360u Ad=18000p As=18000p Pd=820u Ps=820u NRS=0.54 NRD=0.54
.MODEL Di D (IS=10p RS=10 N=1.75 M=0.4 Cjo=6p TT=20n)
.MODEL NA NMOS (LEVEL=49
+VERSION=3.1 CAPMOD=2 MOBMOD=1
+TOX=4E-8 XJ=2.9E-7 NCH=4E15 NSUB=5.33E15 XT=8.66E-8 
+VTH0=1.4 U0= 1300 WINT=2.0E-7 LINT=1E-7 
+NGATE=5E20 RSH=5 JS=3.23E-8 JSW=3.23E-8 CJ=6.8E-8 MJ=0.5 PB=0.95
+CJSW=1.26E-10 MJSW=0.5 PBSW=0.95 PCLM=5
+CGSO=3.4E-10 CGDO=3.4E-10 CGBO=5.75E-10)
.MODEL PA PMOS (LEVEL=49
+VERSION=3.1 CAPMOD=2 MOBMOD=1
+TOX=5E-8 XJ=2.26E-7 NCH=1E15 NSUB=8E14 XT=8.66E-8
+VTH0=-1.65 U0= 400 WINT=1.0E-6 LINT=1E-6
+NGATE=5E20 RSH=15 JS=3.51E-8 JSW=3.51E-8 CJ=5.28E-8 MJ=0.5 PB=0.94
+CJSW=1.19E-10 MJSW=0.5 PBSW=0.94 pCLM=5
+CGSO=4.5E-10 CGDO=4.5E-10 CGBO=5.75E-10)
.ends swbbmblk
