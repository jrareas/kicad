.subckt TL431-R REFIN ANODE CATHODE 
Q8 Q2_C Q2_C R4_N 0 P1
Q9 CATHODE Q7_C Q9_E 0 n1
R8 Q4_E ANODE 800
R9 Q2_E R9_N 4k
Q10 CATHODE R6_P ANODE 0 n1 5
Q11 Q7_C R10_P ANODE 0 n1
C2 R9_N C2_N 20p
R10 R10_P R1_N 1k
C1 CATHODE Q7_C 20p
D1 ANODE Q7_C DIODE
D2 ANODE CATHODE DIODE
R4 CATHODE R4_N 800
Q2 Q2_C Q1_E Q2_E 0 n1
Q3 R9_N C2_N ANODE 0 n1
R5 CATHODE R5_N 800
R6 R6_P Q9_E 150
Q1 CATHODE REFIN Q1_E 0 n1
R7 ANODE R6_P 10k
Q6 Q7_C Q7_C REFIN 0 n1
Q7 Q7_C Q2_C R5_N 0 P1
R1 R3_N R1_N 2.4k
R2 R3_N C2_N 7.2k
Q4 C2_N R1_N Q4_E 0 n1
Q5 R1_N R1_N ANODE 0 n1
R3 Q1_E R3_N 3.28k
.model p1 pnp bf=50
.model n1 npn bf=100 tf=2n cjc=1p is=5e-18 NF=1.07
+ BVbe=2.5 Ibvbe=1p tr=10n ) ;Add Bordodynov
.model diode d rs=1 cjo=2p
.ends
