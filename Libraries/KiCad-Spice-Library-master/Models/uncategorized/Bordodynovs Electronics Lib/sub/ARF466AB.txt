**********
*SRC=ARF466AB;ARF466AB;MOSFETs N;Enh;1.00kV 13.0A 0.900ohms Microsemi PPG RF MOSFET
*SYM=POWMOSN
* 03-02-10 Rev B  GJK PPG Microsemi
.SUBCKT ARF466AB   10 20 30
*     TERMINALS:  D  G  S
M1   1  2  3  3  DMOS L=1U W=1U
LD  10  11  2n Rser=0 Rpar=10
RD  11	1  .90
LG  20  19  2.8n Rser=0 Rpar=10
RG  19  2   .5
D1   5  1   DGD
D2   5  2   DGD
CGS  2  3   2.0n
DSD  3  1   DSUB
RS  29  3   2.25m
LS  30  29  1.5n Rser=0 Rpar=10
.MODEL DMOS  NMOS(LEVEL=3 VMAx=1.04Meg THETA=10.0M ETA=.50M VTO=2.0 KP=1.5)
.MODEL DSUB D (IS=54.0n N=1.50 RS=65.4m BV=1.00k CJO=939p VJ=0.800 M=0.420 TT=304n)
.MODEL DGD  D (IS=100n N=1.5 RS=10m BV=1000 CJO=709P M=.5 VJ=.700 TT=1n)
.ENDS
*****