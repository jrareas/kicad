*relay2
*Summary of State Equations
*M = Mass of plunger
*+X = FLUX
*+B = B Field
*+F = Magnetizing Force
*+I = Coil Current
*+V = Coil Voltage
*+L = Coil Inductance
*+V = LdI/dT
*+I = (1/L)*INTEGRAL(V dt)
*+L = N*X / I
*+X = (L/N)*(1/L)*INTEGRAL(V) dt = (1/N)*INTEGRAL (V) dt
*+FMAG = B*B*A
*+B = X/A
*+F = (X/A)*(X/A)*A = X*X/A
*+FT = FMAG - FSTOP - FFRICTION - FSPRING
*+VELOCITY= 1/M *INTEGRAL(FT) dt
*+POSITION = INTEGRAL(VELOCITY) dt
*
*
*
.SUBCKT relayRe in+ in- out1 out2  PARAMS: RCOIL=100 LCOIL=10m RON=1m ROFF=1G  
+ KSTOP=1E-4 AREA=3 KSPRING=100 KFLUX=3E7 M=.01 KFORCE=.01 Cout=1p
*
C1 OUT1 OUT2 {Cout}
E2 FSTOP 0 TABLE {V(POSITION,0)} = {(0,0) (1, 0) (1.1,1E8) (1.2,2E8)}
E3 FORCE 0 VALUE =
+ {KFORCE*(KFLUX*(V(FLUX))**2/AREA-V(FSTOP)-KSTOP*V(VELOCITY)*V(FSTOP)-(KSPRING*V
+ (POSITION)))}
E5 N8 0 11 0 1
E15 VELOCITY 0 VALUE = {v(F1)*(v(FLUX)>1u)}
GCOIL 11 IN- VALUE =
+ {(1/(LCOIL))*(V(FLUX))*(1+1.1-V(POSITION)+(1.1-V(POSITION))**2)}
RC IN+ 11 RCOIL
S2 OUT1 OUT2 POSITION 0 SR
X6 N8 FLUX INT PARAMS: SCALE=1 VINIT=0 
X8 VELOCITY POSITION INT PARAMS: SCALE=1 VINIT=0 
X9 FORCE f1 INT PARAMS: SCALE={1/M} VINIT=0 
.MODEL SR VSWITCH (RON={RON} ROFF={ROFF} VON=.9 VOFF=.91) 
*
.SUBCKT INT  PinA PinB PARAMS: SCALE=1 VINIT=0 
C 1 0 1 IC = {VINIT}
G1 0 1 PINA 0 {SCALE}
E1 PINB 0 1 0 1
R1 0 1 1E6
.ENDS INT
*
.ENDS relayRe

