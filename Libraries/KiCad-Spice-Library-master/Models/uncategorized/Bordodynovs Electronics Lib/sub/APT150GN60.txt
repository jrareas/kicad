* Microsemi Corporation
* Power Products Group
*
* Part# APT150GN60J.lib
* Created AUG 28, 2007
*
* This SPICE model was created to simulate the characteristics of a typical
* device.  However, as with any simulation model, it is not feasible to model
* exact device performance under all operating conditions.  Microsemi does
* not assume any liability as a result of the use of SPICE models.  Please
* refer to the device datasheet for ratings and detailed device
* characterization.  In addition to simulation, operation of this device
* should be verified by testing in circuit.
*
.SUBCKT APT150GN60 C G E Temp = 25
.PARAM VTO = {7.2+0.006*(125-Temp)}
Q1 83 81 85 QOUT
M1 81 82 83 83 MFIN L=1U W=1U
DDS 83 81 DO
DBE 85 81 DE
RC 85 100 1.96m
LC C 100 2n Rser=0 Rpar=5
RE 83 73 218u
RG 101 82 {1-.005*(125-Temp)}
LG G 101 2n Rser=0 Rpar=5
CGE 82 83 5.1n
EGD 91 0 POLY(1) 82 81 6.50 1
VFB 93 0 0
FFB 82 81 VFB 1
CGD 92 93 31.2n
R1 92 0 1
D1 91 92 DLIM
DHV 94 93 DR
R2 91 94 1
D2 94 0 DLIM
LE 73 E 2n Rser=0 Rpar=5
DLV 94 95 DR 13
RLV 95 0 1
ESD 96 93 POLY(1) 82 81 -6.50 1
MLV 95 96 93 93 SW
.MODEL SW NMOS (LEVEL=3 VTO=0 KP=5)
.MODEL QOUT PNP (IS=236p NF=1.20 BF=5.10
+ CJE=155p TF={3.8n-0.029n*(125-Temp)} XTB=1.3)
.MODEL MFIN NMOS (LEVEL = 3 VMAX=400k
+ THETA=36.9m ETA={1.28m+2.1u*(125-Temp)} VTO={ VTO } KP={6.78+0.0256*(125-Temp)})
.MODEL DR D (IS=23.6p CJO=4n VJ=1.00 M=0.820 )
.MODEL DO D (IS=23.6p BV=599 CJO=16.1p VJ=1.00 M=0.700)
.MODEL DE D (IS=23.6p BV=660 N=2)
.MODEL DLIM D (IS=100N N=.5)
.ENDS APT150GN60
 