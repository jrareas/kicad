.SUBCKT LM2675-ADJ_TRANS VIN FDBACK CBOOT VSWITCH ILIM_OR_ONOFF  GND
I1 VIN ILIM_OR_ONOFF DC 20U 
Ci1 VIN ILIM_OR_ONOFF 20p
XD1 GND ILIM_OR_ONOFF LZEN7_0
BSTARTUP GND ENABLE i=IF( V(ILIM_OR_ONOFF,GND) > 1.4 & V(VIN,GND)>7.90 , V(ILIM_OR_ONOFF,GND) , 0) Rpar=1 Cpar=5n tripdv=50m tripdt=5n NoJacob
B12V  GND REFV i=IF(V(ILIM_OR_ONOFF,GND) > 1.4 , 1.215 , 0) Rpar=1 Cpar=100n tripdv=50m tripdt=5n NoJacob
E_BREF REF5V GND value = { IF((V(VIN,GND) > 7.90 & V(ILIM_OR_ONOFF,GND) > 1.4) , 5 , 0)}
BSSClamp GND SS i=IF(V(VIN) > 3.3 & V(SS) < 5 , 4.5E-6 , 0 ) Cpar=26.37p
BSSCL  GND 361 i=IF(V(101) > 1.1 , 0 , 5  ) Rpar=1 Cpar=5n tripdv=50m tripdt=5n NoJacob
dSS SS 361 DSS
.MODEL DSS D RS=10k Cjo=1f
*.MODEL DSS D Ron=10k Roff=10Meg Vfwd=0.5


XSW2 31 30 VSWITCH NATIONALFET3A_0
E_BVRSEN 102 0 value = { IF( I(VSENSE) > 1.55 , 5 , 0)}
E_BFS2 103 0 value = { IF( I(VSENSE) > 1.829 , 5-(I(VSENSE)*0.12*0.3) , 5)}
X4 VIN CBOOT REG1MA_0
X1 VSWITCH CBOOT LZEN10_0
RREG 50 52 0.33
CREF 52 GND 1N
V3 REF5V 50

VRFB2 FDBACK EA1  0 ;1u

RFB1 EA1 0 1e11
GM1 GND 15 EA1 REFV 4.8E-4
BGM2 GND 17 i=V(REFV,15) * V(13) 
XA1 VIN 13 PWL2
D1 17 REF5V _D 
D2 400 17 _D
VD2 400 Gnd 0.9 
.MODEL _D D cjo=10f
R4 15 16 2K
L1 16 REFV 20mH  Rpar=10k
R5 17 18 15K
C1 18 GND 10NF IC = 2.5
C6 17 18 105P
R10 17 GND 300K
E1 ErrOut Gnd 17 Gnd 0.8 
BSSERR  Gnd 360 i=min(V(SS), V(ErrOut)) Rpar=1 Cpar=5n
X3 RTCT 17 COMPOUT COMPDEL_0

A1 RTCT 0 0 0 0 26 0 0 SCHMITT Vhigh=5 td=10n VT=1.9 VH=1.3
BDISCH RTCT GND i=IF(V(26) < 0.6 & V(REF5V) > 3.2 , .46M , 0)
RT RTCT 33 100K
CT RTCT 9 .039N
VM 9 GND 0

BVM  GND PULSE300N i=IF(I(VM) < 0 , 1 , 0) Rpar=1 cpar=5n tripdv=50m tripdt=5n NoJacob
VSENSE VIN 31
R103 103 33 200
C103 33 GND 1N IC=5
R101 102 101 200
C101 101 GND 1N
X2 35 REF5V 32 GND QB Q FFLOP_0
BR  GND 32 i=IF(V(COMPOUT) > 2 , 5 , IF( V(101) > 1 , 5 , 0)) Rpar=1 Cpar=1n tripdv=50m tripdt=5n NoJacob
BS  GND 35 i= IF(V(PULSE300N) < .5 , 5 , 0) Rpar=1 Cpar=5n
BD  VSWITCH 30 i=IF(V(Qb) < 2.5 & V(SS) > .63 & V(ENABLE) > 1.4 & V(PULSE300N) <.5 , 10 , 0) Rpar=1 Cpar=5n tripdv=50m tripdt=5n NoJacob

.SUBCKT NATIONALFET3A_0 10 20 30
M1   1  2  3  3  DMOS L=1U W=1U
RD  10  1  70.5M
RS  30  3  4.75M
RG  20  2  50
CGS  2  3  336.6P
EGD 12  0  2  1  1
VFB 14  0  0
FFB  2  1  VFB  1
CGD 13 14  0.606N
R1  13  0  1
D1  12 13  DLIM
DDG 15 14  DCGD
R2  12 15  1
D2  15  0  DLIM
DSD  3 10  DSUB
.MODEL DMOS NMOS (LEVEL=3 VMAX=125K THETA=58.1M  VTO=3.1 KP=8.983 ;ETA=2M)
.MODEL DCGD D (VJ=0.6 M=0.68)
.MODEL DSUB D ( IS=1e-15 TT=10p Rs=0.0005 N=.001  )
.MODEL DLIM D ( IS=1e-15 TT=10p Rs=0.0005 N=.001  )
.ENDS

.SUBCKT FFLOP_0     1    2  11 12  5     6
Vr R 0 pwl 0 0 10.1u 0 10.105u 3
A1 7 0 4 0 2 8 0 0 AND Ref=1.5 Vhigh=3.5 Vlow=0.3 Rout=400 Cout=20p Trise=5n
A2 8 0 3 R 10 9 0 0 AND Ref=1.5 Vhigh=3.5 Vlow=0.3 Rout=400 Cout=20p Trise=5n
A3 1 0 8 0 10 7 0 0 AND Ref=1.5 Vhigh=3.5 Vlow=0.3 Rout=400 Cout=20p Trise=5n
A4 4 0 9 R 1 10 0 0 AND Ref=1.5 Vhigh=3.5 Vlow=0.3 Rout=400 Cout=20p Trise=5n
A5 4 0 7 0 6 5 0 0 AND Ref=1.5 Vhigh=3.5 Vlow=0.3 Rout=400 Cout=20p Trise=5n
A6 5 0 10 R 3 6 0 0 AND Ref=1.5 Vhigh=3.5 Vlow=0.3 Rout=400 Cout=20p Trise=5n
A7 11 0 0 R 0 4 0 0 and Ref=1.5 Vhigh=3.5 Vlow=0.3 Rout=100 Cout=10p Trise=5n
A8 12 0 0 R 0 3 0 0 and Ref=1.5 Vhigh=3.5 Vlow=0.3 Rout=100 Cout=10p Trise=5n
.ENDS


.SUBCKT COMPDEL_0 1 2 3
B1 0 5 i =IF(V(1) > V(2) , 1V , 0)/100 Rpar=100 Cpar=0.1n tripdv=50m tripdt=5n NoJacob
B2 0 3 i=IF(V(5) > 500m , 5V , 0)/100 Rpar=100 Cpar=250p tripdv=50m tripdt=5n NoJacob
.ENDS

.SUBCKT LZEN7_0 1 2
D7 1 2 DZEN
.MODEL _D D
.MODEL DZEN D(RS=3.5305 BV=6.9294 CJO=313.27P TT=50N M=.33 VJ=.75 IS=1E-11  N=1.27 IBV=20MA)
.ENDS

.SUBCKT LZEN10_0 1 2
D1 1 2 DZEN
.MODEL _D D
.MODEL DZEN D(RS=8.4831 BV=9.8303 CJO=233.41P TT=50N M=.33 VJ=.75 IS=1E-11  N=1.27 IBV=20MA)
.ENDS

.SUBCKT REG1MA_0 1     2
JFET 1 2 2 JREG1MA
.MODEL JREG1MA NJF (VTO=-1.35 BETA=.67M LAMBDA=1.0M RD=49  RS=125 IS=1.56F PB=1 FC=.5 CGS=5P CGD=3.8P)
.ENDS

.SUBCKT FEEDBACK_BLOCK_WB_FEEDBACK_BLOCK_FIXED_OUTPUT 1 2
Vzero1 1 2 0
.ENDS


.SUBCKT PWL2 1 2
E_PWL  2 0 TABLE = {V(1,0)} ((10 1.48M) (20 0.673M) (40 0.48M))
.ENDS

.subckt d_d1 1 2
d1 1 2 dd1
.model dd1 d
+ is=1e-015
+ tt=1e-011
+ rs=0.05
+ n=0.1 CJO=10f
.ends d_d1

.ENDS LM2675-ADJ_TRANS
