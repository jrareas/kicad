*
* CD4060B model incorporating CD4020B model, with additions/mods by Geoff Western (aka Alec_t)
*
* 14-stage Binary Ripple Counter with clock oscillator facility, but with Q1-3 and Q11 outputs not accessible.
* Clk->Q1 tpd 180ns
* Qn->Qn+1 tpd=90n
* Rst->Q tpd=140ns
.SUBCKT CD4060Bg Clk Cx Rst Rx Q10 Q12 Q13 Q14 Q4 Q5 Q6 Q7 Q8 Q9 VDD VGND  vdd1={vdd} speed1={speed} tripdt1={tripdt}
.param td1=1e-9*(180-90-40-10)*5/vdd1*speed1
.param td2=0
.param td3=1e-9*(90)*5/vdd1*speed1
*
XIN1  Rst  MRi  VDD VGND  CD40_IN_1  vdd2=vdd1  speed2=speed1  tripdt2=tripdt1
XIN2  Clk  CPi  VDD VGND  CD40_IN_1  vdd2=vdd1  speed2=speed1  tripdt2=tripdt1
*
AR   MRi 0   0 0 0   MRn MRp 0  BUF  tripdt=tripdt1  td=td2
AR0  MRi 0   0 0 0   MR0n  0 0  BUF  tripdt=tripdt1  td=td1
AC   CPi 0   0 0 0   0   C1p 0  BUF  tripdt=tripdt1  td=td1
ACP  C1p MR0n 0 0 0  CPn   0 0  AND  tripdt=tripdt1
*
* Added parts
XIN3 Rx Rxi VDD VGND CD40_IN_1  vdd2=vdd1  speed2={3*speed1}  tripdt2=tripdt1 ; (speed-up prevents ringing)
XIN4 Cx Cxi VDD VGND CD40_IN_S_1  vdd2=vdd1  speed2={3*speed1}  tripdt2=tripdt1
XOUT1 CPn Rx VDD VGND  CD40_OUT_1X  vdd2=vdd1 speed2=speed1  tripdt2=tripdt1
XOUT2 RBn Cx VDD VGND  CD40_OUT_1X  vdd2=vdd1 speed2=speed1  tripdt2=tripdt1
ACX Cxi 0 0 0 0 DFi 0 0 BUF tripdt=tripdt1  td=td1
ARX Rxi 0 0 0 0 RBn 0 0 BUF tripdt=tripdt1  td=td1
*
AFF1   Q1n  0 DFi  0 MRp  Q1n  Q1p  0  DFLOP  tripdt=tripdt1  td=td3	; (input changed)
AFF2   Q2n  0 Q1n  0 MRp  Q2n  Q2p  0  DFLOP  tripdt=tripdt1  td=td3
AFF3   Q3n  0 Q2n  0 MRp  Q3n  Q3p  0  DFLOP  tripdt=tripdt1  td=td3
AFF4   Q4n  0 Q3n  0 MRp  Q4n  Q4p  0  DFLOP  tripdt=tripdt1  td=td3
AFF5   Q5n  0 Q4n  0 MRp  Q5n  Q5p  0  DFLOP  tripdt=tripdt1  td=td3
AFF6   Q6n  0 Q5n  0 MRp  Q6n  Q6p  0  DFLOP  tripdt=tripdt1  td=td3
AFF7   Q7n  0 Q6n  0 MRp  Q7n  Q7p  0  DFLOP  tripdt=tripdt1  td=td3
AFF8   Q8n  0 Q7n  0 MRp  Q8n  Q8p  0  DFLOP  tripdt=tripdt1  td=td3
AFF9   Q9n  0 Q8n  0 MRp  Q9n  Q9p  0  DFLOP  tripdt=tripdt1  td=td3
AFF10  Q10n 0 Q9n  0 MRp  Q10n Q10p 0  DFLOP  tripdt=tripdt1  td=td3
AFF11  Q11n 0 Q10n 0 MRp  Q11n Q11p 0  DFLOP  tripdt=tripdt1  td=td3
AFF12  Q12n 0 Q11n 0 MRp  Q12n Q12p 0  DFLOP  tripdt=tripdt1  td=td3
AFF13  Q13n 0 Q12n 0 MRp  Q13n Q13p 0  DFLOP  tripdt=tripdt1  td=td3
AFF14  Q14n 0 Q13n 0 MRp  Q14n Q14p 0  DFLOP  tripdt=tripdt1  td=td3
*
* (Q1 output deleted)
XOUT4  Q4p  Q4   VDD VGND  CD40_OUT_1X  vdd2=vdd1 speed2=speed1  tripdt2=tripdt1
XOUT5  Q5p  Q5   VDD VGND  CD40_OUT_1X  vdd2=vdd1 speed2=speed1  tripdt2=tripdt1
XOUT6  Q6p  Q6   VDD VGND  CD40_OUT_1X  vdd2=vdd1 speed2=speed1  tripdt2=tripdt1
XOUT7  Q7p  Q7   VDD VGND  CD40_OUT_1X  vdd2=vdd1 speed2=speed1  tripdt2=tripdt1
XOUT8  Q8p  Q8   VDD VGND  CD40_OUT_1X  vdd2=vdd1 speed2=speed1  tripdt2=tripdt1
XOUT9  Q9p  Q9   VDD VGND  CD40_OUT_1X  vdd2=vdd1 speed2=speed1  tripdt2=tripdt1
XOUT10 Q10p Q10  VDD VGND  CD40_OUT_1X  vdd2=vdd1 speed2=speed1  tripdt2=tripdt1
* (Q11 output deleted)
XOUT12 Q12p Q12  VDD VGND  CD40_OUT_1X  vdd2=vdd1 speed2=speed1  tripdt2=tripdt1
XOUT13 Q13p Q13  VDD VGND  CD40_OUT_1X  vdd2=vdd1 speed2=speed1  tripdt2=tripdt1
XOUT14 Q14p Q14  VDD VGND  CD40_OUT_1X  vdd2=vdd1 speed2=speed1  tripdt2=tripdt1
.ENDS CD4060Bg
*
*================================================================
*
*   INPUT FILTERS
*
.MODEL CD40DIO1 D(Is=1e-12 Rs=100)
*
.SUBCKT  CD40_IN_0  in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2}
.param vt1=0.5
.param gain=(1/{vdd3})
*
*D1 0   in  CD40DIO1
*D2 in VDD  CD40DIO1
R2 in VGND 1e8
E1 out20 0 in VGND {gain}
*B1 out20 0 V=LIMIT(0,V(in)/max(V(VDD,VGND),1),1)
AE1  out20 0 0 0 0  0 out 0  BUF  ref={vt1}  vhigh=1  tripdt={tripdt3}
.ends
*
*
.SUBCKT  CD40_IN_1  in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2}
* 3ns input delay
*.param Cval = 0.55e-12*4/({vdd3}-0.5)*{speed3}
* 10ns delay @5V
.param Cval = 1.8e-12*5/{vdd3}*{speed3}
.param vt1=0.5
.param gain=(1/{vdd3})
*
*D1 0   in  CD40DIO1
*D2 in VDD  CD40DIO1
R1 in out10 10k
C1 out10 VGND {Cval}
R2 in VGND 1e8
**E1 out20 0 out10 VGND {gain}
B1 out20 0 V=LIMIT(0,V(out10,VGND)*{gain},1)
*B1 out20 0 V=LIMIT(0,V(out10,VGND)/max(V(VDD,VGND),1),1)
AE1  out20 0 0 0 0  0 out 0  BUF  ref={vt1}  vhigh=1  tripdt={tripdt3}
.ends
*
*
* Schmitt-input; 2.9V/2.1V @5V
.SUBCKT  CD40_IN_S_1  in out  VDD VGND  vdd3={vdd2}  speed3={speed2}  tripdt3={tripdt2}
.param Cval = 1.8e-12*5/{vdd3}*{speed3}
.param vt1=2.5/5
.param vh1=0.4/5
.param gain=(1/{vdd3})
*
*D1 0   in  CD40DIO1
*D2 in VDD  CD40DIO1
R1 in out10 10k
C1 out10 VGND {Cval}
R2 in VGND 1e8
**E1 out20 0 out10 VGND {gain}
B1 out20 0 V=LIMIT(0,V(out10,VGND)*{gain},1)
*B1 out20 0 V=LIMIT(0,V(out10,VGND)/max(V(VDD,VGND),1),1)
AE1  out20 0 0 0 0  0 out 0  SCHMITT  vt={vt1} vh={vh1} vhigh=1  tripdt={tripdt3}
.ends
*
*
*======================================================================
*
* OUTPUT DRIVERS, LEVEL TRANSLATORS
*
*
* Tristate switch
.MODEL SW_HC1 SW(Vt=0.5 Ron=1 Roff=1e6)
.MODEL SW_HC2 SW(Vt=0.5 Ron=1 Roff=1e6)
*
.MODEL DIO2 D(Is=1e-12 Rs=10)
*
* Standard output driver
.SUBCKT  CD40_OUT_1X  in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2}
.param trise1=80e-9*5.0/{vdd3}*{speed3}
.param Rout=500*5.0/{vdd3}*{speed3}
*
AE1  in 0 0 0 0  0 out10 0  BUF  tripdt={tripdt3}  trise={trise1}
*
E1 out20 VGND out10 0 {vdd3}
*B1 out20 VGND V=V(out10)*V(VDD,VGND)
Rout out20 out {Rout}
*D1 0   out  DIO2
*D2 out VDD  DIO2
.ends
*
*
*
* Strong output driver
.SUBCKT  CD40_OUT_2X  in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2}
.param trise1=80e-9*5/{vdd3}*{speed3}
.param Rout=250*5.0/{vdd3}*{speed3}
*
AE1  in 0 0 0 0  0 out10 0  BUF  tripdt={tripdt3}  trise={trise1}
*
E1  out20 VGND out10 0 {vdd3}
*B1 out20 VGND V=V(out10)*V(VDD,VGND)
Rout  out20 out {Rout}
*D1 0   out  DIO2
*D2 out VDD  DIO2
.ends
*
*
*
* Tristate output driver
.SUBCKT  CD40_OUT_TS_2X  en in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2}
.param trise1=80e-9*5.0/{vdd3}*{speed3}
.param Rout=250*5/{vdd3}*{speed3}
*
A1  in 0 0 0 0  0 out10 0  BUF  tripdt={tripdt3}  trise={trise1}
*
E1  out20 VGND out10 0 {vdd3}
*B1 out20 VGND V=V(out10)*V(VDD,VGND)
Rout  out20 out30 {Rout}
SW1  out30 out en 0 SW_HC1
*D1 0   out  DIO2
*D2 out VDD  DIO2
.ends
*
*
*
* Open drain output driver
.SUBCKT  CD40_OUT_OD_1X  in out  VDD VGND  vdd3={vdd2} speed3={speed2}  tripdt3={tripdt2}
.param trise1=80e-9*5/{vdd3}*{speed3}
.param Rout=500*5/{vdd3}*{speed3}
*
A1  in 0 0 0 0   out10 0 0  BUF  tripdt={tripdt3}  trise={trise1}
*
Rout out30 VGND {Rout}
SW1  out30 out out10 0 SW_HC2
 *Alternative real output stage from CD40U04 would replace Rout and SW1
 *E1  out20 VGND out10 0 {vdd3}
 *Rout  out20 out30 {Rout}
 *MN1 out out30 VGND VGND  MHCNEN W=140U L=2.4U AD=200P AS=300P PD=10U PS=130U
 *MN2 out out30 VGND VGND  MHCNEN W=140U L=2.4U AD=200P AS=300P PD=10U PS=130U
 *MN3 out out30 VGND VGND  MHCNEN W=140U L=2.4U AD=200P AS=300P PD=10U PS=130U
*D1 0   out  DIO2
*D2 out VDD  DIO2
.ends
*
*
