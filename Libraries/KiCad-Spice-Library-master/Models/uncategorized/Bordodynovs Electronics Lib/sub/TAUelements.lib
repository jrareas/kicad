* TAUelements.lib Kubov V.I. 27.02.2010

* ------------------ Line ------------------
.subckt amp in out
E1 out 0 in 0 {K}
.param K=1
.ends amp

.subckt ampInv in out
E1 out 0 in 0 {-K}
.param K=1
.ends ampInv

.subckt sum in1 in2 out
E1 N001 0 in1 0 1
E2 out N001 in2 0 1
.ends sum

.subckt subtr inP inN out
E1 N001 0 inN 0 -1
E2 out N001 inP 0 1
.ends subtr

.subckt mul in1 in2 out
B1 out 0 V=V(in1)*V(in2)
R1 in1 0 1G
R2 in2 0 1G
.ends mul

.subckt div in1 in2 out
B1 out 0 V=V(in1)/V(in2)
R1 in1 0 1G
R2 in2 0 1G
.ends div

* -------------- Laplace --------------

.subckt Laplace0 in out
R1 in 0 1G
B1 out 0 V=V(in) Laplace(W(s))
;.func W(p) {1/(1+p*1e-3)}
.ends Laplace0

.subckt Laplace1 in out
R1 in 0 1G
B1 out 0 V=V(in) Laplace(W1(s))
.ends Laplace1

.subckt Laplace2 in out
R1 in 0 1G
B1 out 0 V=V(in) Laplace(W2(s))
.ends Laplace2

.subckt intDelay in out
E1 out 0 in 0 Laplace(1/(1+s*{T}))
.param T=1e-3
.ends intDelay

.subckt difDelay in out
E1 out 0 in 0 Laplace(s*{T}/(1+s*{T}))
.param T=1e-3
.ends difDelay

.subckt int in out
G1 0 out in 0 1
C1 out 0 {T}
.param T=1m
.ends int

.subckt dif in out
E1 out 0 in 0 Laplace(s*{T})
.param T=1m
.ends dif

.subckt osc in out
E1 out 0 in 0 Laplace(1/(1+s*{T/Q}+(s*{T})**2))
.param Q=3 T=1m
.ends osc

.subckt oscP in out
E1 out 0 in 0 Laplace(1/(1+s*{q/P}+(s/{P})**2))
.param q=0.3 P=1K
.ends oscP

* -------------------- No Line ------------------

.subckt Limit in out
B1 out 0 V=limit(V(in),{Lo},{Hi})
R1 in 0 1G
.params Lo=0 Hi=1
.ends Limit


.subckt Dead in out
B1 out 0 V=max(V(in)-{u},min(V(in)-{d},0))
R1 in 0 1G
.params u=0.5 d=-0.5
.ends Dead

.subckt DeadB in out
B1 out 0 V=max(V(in)-{u}+{L},min(V(in)-{d}+{L},{L}))
R1 in 0 1G
.params L=0 u=0.5 d=-0.5
.ends DeadB

.subckt func0 in out
B1 out 0 V=Y(V(in))
R1 in 0 1G
;.func Y(x){x*x}
.ends func0

.subckt func1 in out
B1 out 0 V=Y1(V(in))
R1 in 0 1G
.ends func1

.subckt func2 in out
B1 out 0 V=Y2(V(in))
R1 in 0 1G
.ends func2

.subckt sinX in out
B1 out 0 V=sin(V(in))
R1 in 0 1G
.ends sinX

.subckt cosX in out
B1 out 0 V=cos(V(in))
R1 in 0 1G
.ends cosX

.subckt expX in out
B1 out 0 V=exp(V(in))
R1 in 0 1G
.ends expX

.subckt LnX in out
B1 out 0 V=Ln(V(in))
R1 in 0 1G
.ends LnX

.subckt powN in out
B1 out 0 V=pow(V(in),{N})
R1 in 0 1G
.param N=2
.ends powN

* --------------- Hystiresis-------------------

.subckt hyster in out
Sw 0 out in 0 SwModel
R1 out 0 1G
.model SwModel  Sw(Vt={t} Vh={h} Ron=1m Vser=1)
.params t=0 h=0.5
.ends hyster

.subckt hysterB in out
Sw 0 S in 0 SwModel
R1 S 0 1G
B1 out 0 V=if(V(S),{Hi},{Lo})
.model SwModel  Sw ( Vt={(u+d)/2} Vh={(u-d)/2} Ron=1m Vser=1 )
.params d=-0.5 u=0.5 Lo=-1 Hi=1
.ends hysterB

* ----------- Signal Generators ---------------

.subckt genTrap p
V1 p 0 PULSE(0 1 {dT} {Tr} {Tf} {Ton} {Period}) AC 1
.params dT=1m Tr=1m Ton=1m Tf=1m Period=100m
.ends genTrap

.subckt genTrapB p
V1 p 0 PULSE({Lo} {Hi} {dT} {Tr} {Tf} {Ton} {Period}) AC 1
.params dT=1m Tr=1m Ton=1m Tf=1m Period=100m Lo=0 Hi=1
.ends genTrapB

.subckt genPulse p
V1 p 0 PULSE(0 1 {dT} 1n 1n {Ton} {Period}) AC 1
.params dT=1m Ton=10m Period=100m
.ends genPulse

.subckt genPulseB p
V1 p 0 PULSE({Lo} {Hi} {dT} 1n 1n {Ton} {Period}) AC 1
.params dT=1m Ton=10m Period=100m Lo=0 Hi=1
.ends genPulseB

.subckt genLine p
V1 p 0 PULSE(0 1 {dT} {Tr} {Tf} 0 {Period}) AC 1
.params dT=0m Tr=50m Tf=30m Period=100m
.ends genLine

.subckt genLineB p
V1 p 0 PULSE({Lo} {Hi} {dT} {Tr} {Tf} 0 {Period}) AC 1
.params dT=0m Tr=50m Tf=30m Period=100m Lo=0 Hi=1
.ends genLineB

.subckt genSin p
V1 p 0 SINE(0 1 {F}) AC 1 0
.param F=1K
.ends genSin

.subckt genSinA p
V1 p 0 SINE(0 {A} {F}) AC 1 0
.param A=2 F=1K
.ends genSinA

.subckt const p 
V1 p 0 {C}
.ends const

.subckt genTime p
B1 p 0 V=time
.ends genTime