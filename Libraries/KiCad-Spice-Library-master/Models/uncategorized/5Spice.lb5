*5Spice internal library file
*version 1.3
*  >> DO NOT MOVE OR MODIFY THIS FILE <<

*!STOP_INDEXING

*=============================
* VOLTAGE AND CURRENT CONTROLLED SWITCHES 

.SUBCKT SWITCHI 1 2 3 4 Params: VON=10 VOFF=0 RON=10 ROFF=1e8
Vs  3  4  0
Hs  5  0  Vs  1  ; CCVS
xsw 1 2 5 0 SWITCHV  Params: VON={VON} VOFF={VOFF} RON={RON} ROFF={ROFF}
.ENDS SwitchI

*------------------------------------------------------

*   4/17/03 with  simplifications
*VSwitch replacement: voltage variable resistor
*                 Rout1
*                 |  Rout2
*                 |  |  VcPos
*                 |  |  |  VcNeg
*                 |  |  |  |
.SUBCKT  SWITCHV  1  2  3  4  PARAMS:  VON=10 VOFF=0 RON=10 ROFF=1e8
*PSpice switch equation with NO resistor noise modeled.  better convergence
Rc  3  4  1e9  ;PSpice does this. not needed otherwise
Vs  9  2  0
Bpol pol  0 v = ({VON - VOFF}) >= 0 ? 5 : -5  ;determine polarity of control voltage
Bctrl  ctrl 0  v = V(3)-V(4)

Brr  1  9   v =  ((V(pol)>0 & V(ctrl) > {VON}) |  (V(pol)<0 & V(ctrl) < {VON}))
+             ?     i(Vs) * {RON}
+             :  ((V(pol)>0 & V(ctrl) < {VOFF}) |  (V(pol)<0 & V(ctrl) > {VOFF}))
+             ?     i(Vs) * {ROFF}
+             :   i(Vs) * exp( ln({(RON*ROFF)^0.5}) 
+    + 3 * ln({RON/ROFF}) * (V(ctrl) - {(VON+VOFF)/2})  / {2 * (VON - VOFF)}
+    - (2 * ln({RON/ROFF}) * ((V(ctrl) - {(VON+VOFF)/2}) / {VON - VOFF})^3)   )
.ENDS SwitchV

*==========================
* BEHAVIORAL LOGIC ELEMENTS

** INVERTOR
.SUBCKT INV 1 2  {LTHRES=1.0  LLEVEL=5.0  CDELAY=50P}
B1 3 0 V= V(1) > {LTHRES} ? 0 : {LLEVEL}
RD 3 2 100
CD 2 0 {CDELAY}
.ENDS INV

** 2 INPUT COMPARATOR
.SUBCKT COMPARE 1 2 3  {LTHRES=1.0  LLEVEL=5.0  CDELAY=10P}
*                            + - OUT
B1 4 0 V= V(1) > V(2) ? {LLEVEL} : 0
RD 4 3 100
CD 3 0 {CDELAY}
.ENDS COMP

** 2 INPUT OR
.SUBCKT OR2 1 2 3  {LTHRES=1.0  LLEVEL=5.0  CDELAY=10P}
B1 4 0 V= ((V(1)>{LTHRES}) | (V(2)>{LTHRES})) ? {LLEVEL} : 0
RD 4 3 100
CD 3 0 {CDELAY}
.ENDS OR2

** 2 INPUT NOR
.SUBCKT NOR2 1 2 3  {LTHRES=1.0  LLEVEL=5.0  CDELAY=10P}
B1 4 0 V= ((V(1)>{LTHRES}) | (V(2)>{LTHRES})) ? 0 : {LLEVEL}
RD 4 3 100
CD 3 0 {CDELAY}
.ENDS NOR2

** 2 INPUT XOR
.SUBCKT XOR2 1 2 3  {LTHRES=1.0  LLEVEL=5.0  CDELAY=10P}
B1 4 0 V= ((V(1)>{LTHRES}) & (V(2)<{LTHRES})) | 
+                 ((V(1)<{LTHRES}) & (V(2)>{LTHRES})) ? {LLEVEL} : 0
RD 4 3 100
CD 3 0 {CDELAY}
.ENDS XOR2

** 2 INPUT AND
.SUBCKT AND2 1 2 3   {LTHRES=1.0  LLEVEL=5.0  CDELAY=10P}
B1 4 0 V= ((V(1)>{LTHRES}) & (V(2)>{LTHRES})) ? {LLEVEL} : 0
RD 4 3 100
CD 3 0 {CDELAY}
.ENDS AND2

** 2 INPUT NAND
.SUBCKT NAND2 1 2 3   {LTHRES=1.0  LLEVEL=5.0  CDELAY=10P}
B1 4 0 V= ((V(1)>{LTHRES}) & (V(2)>{LTHRES})) ? 0 : {LLEVEL}
RD 4 3 100
CD 3 0 {CDELAY}
.ENDS NAND2

** RS FLIPFLOP,  RPA new design 4/29/03
.SUBCKT RSFLOP 1 2 3 4   {LTHRES=1.0  LLEVEL=5.0  CDELAY=10P}
*                       S R Q Q\
BQ    5 0 V= (V(5)>{LThres} & V(2)<{LTHRES}) | V(1)>{LTHRES}
+  ? {LLEVEL}  :  0
BQQ  6 0 V= V(5)<{LThres} | V(2)>{LThres} ? {LLEVEL} : 0
RD1   5 3 100
CD1   3 0 {CDELAY}
RD2   6 4 100
CD2   4 0 {CDELAY}
.ENDS RSFLOP
